/** 
 *  CREATE HUFF EXERCISE
 *  
 *  The task is to deploy this contract with a function `makeContract()` which
 *  when called creates a contract that when called with any data returns
 *  `0xcaffe`.

 *  The call to `makeContract` should return the address of the newly created
 *  contract.
 *
 *  NOTICE: The contract should revert when an unrecognized function is called
 */

 #define function makeContract() payable returns(address)
 /* initialization code
 00 PUSH1        60 0c
 02 PUSH1        60 0c
 04 PUSH1        60 00
 06 CODECOPY     39
 07 PUSH1        60 0c
 09 PUSH1        60 00
 0b RETURN       f3
 0c PUSH3        62 0caffe
 10 PUSH1        60 00
 12 MSTORE       52
 13 PUSH1        60 20 
 15 PUSH1        60 00 
 17 RETURN       f3

initialization code: "600c600c600039600c6000f3620caffe60005260206000f3"
 */
 
 #define macro MAIN() = takes(0) returns(0) {

    // Get the fn sig from calldata
    push0 calldataload 0xE0 shr    // [fn_sig (calldata @ push0 >> 0xE0)]

    __FUNC_SIG(makeContract)       // [sel(makeContract), fn_sig]
    eq deploy jumpi                // []
    push0 push0 revert             // []
    deploy:

        __RIGHTPAD("600c600c600039600c6000f3620caffe60005260206000f3")
        push0 mstore                // []
        0x20 push0 callvalue create // [address]
        push0 mstore                // []
        0x20 push0 return           // []
 }
